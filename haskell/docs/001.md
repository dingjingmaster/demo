## 变量和变量声明
```hs
x :: Int

x = 3

-- 这是注释

{- 这也是注释 -}
```
上面的代码声明了一个Int类型的变量x(`::`读作“has type”)，并声明x的值为3。注意，这将永远是x的值(至少在这个特定的程序中是这样)。x的值不能修改。

试着取消下面一行的注释;它会生成一个错误，类似于多次声明' x'。

```hs
-- x = 4
```
> 在Haskell中，变量不是可变框;它们只是值的名称!换句话说，`=`不像在许多其他语言中那样表示“赋值”。相反，`=`表示定义，就像在数学中一样。

## 基础类型

- Int
- Integer
- Double
- Float
- Booleans
- Char
- Strings

`Int`：Haskell语言标准保证int至少可以容纳\(\pm 2^{29}\)的值，但确切的大小取决于您的体系结构。例如，在我的64位机器上，范围是\(\pm 2^{63}\)。您可以通过评估以下内容在您的机器上找到该范围:
```hs
biggestInt, smallestInt :: Int
biggestInt  = maxBound
smallestInt = minBound
```
> 注意：Haskell 使用驼峰命名变量

另一方面，Integer类型仅受计算机内存量的限制。

```hs
-- Arbitrary-precision integers
n :: Integer
n = 1234567890987654321987340982334987349872349874534

reallyBig :: Integer
reallyBig = 2^(2^(2^(2^2)))

numDigits :: Int
numDigits = length (show reallyBig)
```

对于浮点数，有Double:
```hs
-- Double-precision floating point
d1, d2 :: Double
d1 = 4.5387
d2 = 6.2831e-4
```
还有一种单精度浮点数类型Float。

最后，还有布尔值、字符和字符串:

```hs
-- Booleans
b1, b2 :: Bool
b1 = True
b2 = False

-- Unicode characters
c1, c2, c3 :: Char
c1 = 'x'
c2 = 'Ø'
c3 = 'ダ'

-- Strings are lists of characters with special syntax
s :: String
s = "Hello, Haskell!"
```
## GHCI

GHCi是一个交互式Haskell REPL(读取-执行-打印-循环)，随GHC一起提供。在GHCi提示符下，您可以计算表达式，使用:load (:l)加载Haskell文件(并使用:reload (:r)重新加载它们)，使用:type (:t)询问表达式的类型，以及许多其他事情(尝试:?获取命令列表)。

## Arithmetic

尝试评估GHCi中的以下表达式:
```hs
ex01 = 3 + 2
ex02 = 19 - 27
ex03 = 2.35 * 8.6
ex04 = 8.7 / 3.1
ex05 = mod 19 3
ex06 = 19 `mod` 3
ex07 = 7 ^ 222
ex08 = (-3) * (-7)
```
注意'反引号'是如何将函数名变成中缀运算符的。还要注意，负数通常必须用圆括号括起来，以避免将负号解析为减法。(是的，这很丑。我很抱歉。)

加法只能在相同数字类型的值之间进行，Haskell不进行隐式转换。必须显式地转换为:
- fromIntegral:从任何整数类型(Int或Integer)转换为任何其他数字类型。
- round, floor, ceiling:将浮点数转换为Int或Integer。

```hs
-- badArith2 = i / i
-- 这是一个错误，因为/只执行浮点除法。对于整数除法，我们可以使用div。
```

如果你已经习惯了其他对数字类型进行隐式转换的语言，那么一开始你可能会觉得这有点拘礼和烦人。然而，我保证你会习惯的——假以时日，你甚至会开始欣赏它。隐式数字转换鼓励对数字代码的草率思考。

## Boolean 逻辑

正如您所期望的那样，布尔值可以与(&&)(逻辑and)、(||)(逻辑or)和 not 组合。例如,

```hs
ex11 = True && False
ex12 = not (False || True)
```
可以用(==)和(/=)来比较相等的东西，也可以用(<)、(>)、(<=)和(>=)来比较顺序。

```hs
ex13 = ('a' == 'a')
ex14 = (16 /= 3)
ex15 = (5 > 3) && ('p' <= 'q')
ex16 = "Haskell" > "C++"
```

Haskell也有if表达式:如果b则t，否则f是一个表达式，如果布尔表达式b的值为True，则f的值为t，如果b的值为False。请注意，if表达式与if语句非常不同。例如，对于if语句，else部分可以是可选的;省略的else子句表示“如果测试结果为False，则不执行任何操作”。另一方面，*对于if表达式，else部分是必需的，因为if表达式必须产生某个值*。

惯用的Haskell并不经常使用if表达式，而是经常使用模式匹配或警卫(请参阅下一节)。

## 定义基本函数

```hs
-- Compute the sum of the integers from 1 to n.
sumtorial :: Integer -> Integer
sumtorial 0 = 0
sumtorial n = n + sumtorial (n-1)
```
> 注意：函数类型的语法:sumtoror:: Integer -> Integer表示sumtoror是一个接受Integer作为输入并产生另一个Integer作为输出的函数。

按照从上到下的顺序检查每个子句，并选择第一个匹配的子句。例如，sumitor 0的计算结果为0，因为匹配了第一个子句。sumtor 3与第一个子句不匹配(3不为0)，因此尝试第二个子句。像n这样的变量可以匹配任何东西，所以第二个子句匹配并且sumatorial 3的计算结果为3 + sumatorial(3-1)(然后可以进一步计算)。

还可以根据使用保护的任意布尔表达式做出选择。例如:
```hs
hailstone :: Integer -> Integer
hailstone n
  | n `mod` 2 == 0 = n `div` 2
  | otherwise      = 3*n + 1
```

可以将任意数量的警卫与函数定义的每个子句相关联，每个子句都是布尔表达式。如果子句的模式匹配，则按从上到下的顺序计算警卫，并选择第一个计算为True的警卫。如果没有一个守卫的值为True，则继续匹配下一个子句。

```hs
foo :: Integer -> Integer
foo 0 = 16
foo 1 
  | "Haskell" > "C++" = 3
  | otherwise         = 4
foo n
  | n < 0            = 0
  | n `mod` 17 == 2  = -43
  | otherwise        = n + 3
```

## Pairs

```hs
p :: (Int, Char)
p = (3, 'x')
```

注意，(x,y)表示法既用于对的类型，也用于对的值。

可以使用模式匹配再次提取pair中的元素:

```hs
sumPair :: (Int,Int) -> Int
sumPair (x,y) = x + y
```
Haskell也有三元组、四元组等，但你不应该使用它们。下周我们将看到，有更好的方法将三个或更多的信息打包在一起。

## 使用函数和多个参数

要将函数应用于某些参数，只需在函数后面列出参数，用空格分隔，如下所示:

```hs
f :: Int -> Int -> Int -> Int
f x y z = x + y + z
ex17 = f 3 17 8
```

上面的示例将函数f应用于三个参数3、17和8。还要注意带有多个参数的函数类型的语法，如Arg1Type -> Arg2Type ->…- > ResultType。这对你来说可能很奇怪(确实应该!)为什么有这么多箭头?如果f的类型是Int Int -> Int不是更有意义吗?实际上，这种语法并不是偶然的:它之所以是这样，是有一个非常深刻而美丽的原因的，我们将在几周后学习到;现在你只要相信我的话就行了!

请注意，函数应用程序具有比任何中缀操作符更高的优先级。所以这样写是不对的:
```hs
f 3 n+1 7
```

如果你打算将n+1作为第二个参数传递给f，因为它解析为
```hs
(f 3 n) + (1 7).
```

实际上应该这么写：
```hs
f 3 (n+1) 7.
```

## List

列表是Haskell中最基本的数据类型之一。

```hs
nums, range, range2 :: [Integer]
nums   = [1,2,3,19]
range  = [1..100]
range2 = [2,4..100]
```
Haskell(和Python一样)也有列表推导式;你可以在LYAH中读到。

字符串只是字符列表。也就是说，String只是[Char]的缩写，而字符串字面语法(被双引号包围的文本)只是Char字面量列表的缩写。

```hs
-- hello1 and hello2 are exactly the same.

hello1 :: [Char]
hello1 = ['h', 'e', 'l', 'l', 'o']

hello2 :: String
hello2 = "hello"

helloSame = hello1 == hello2
```
这意味着用于处理列表的所有标准库函数也可以用于处理字符串。

## 构建列表

emptyList = []

其他列表使用cons操作符`(:)`从空列表构建。Cons接受一个元素和一个列表，并生成一个新列表，该列表将该元素放在前面。

```hs
ex18 = 1 : []
ex19 = 3 : (1 : [])
ex20 = 2 : 3 : 4 : []
ex21 = [2,3,4] == 2 : 3 : 4 : []
```
我们可以看到，[2,3,4]表示法只是对2:3:4:[]的方便速记。还要注意，这些实际上是单链表，而不是数组。

```hs
-- Generate the sequence of hailstone iterations from a starting number.
hailstoneSeq :: Integer -> [Integer]
hailstoneSeq 1 = [1]
hailstoneSeq n = n : hailstoneSeq (hailstone n)
```
## 列表上的函数

我们可以使用模式匹配在列表上编写函数。

```hs
-- Compute the length of a list of Integers.
intListLength :: [Integer] -> Integer
intListLength []     = 0
intListLength (x:xs) = 1 + intListLength xs
```

第一个子句说空列表的长度为0。第二个子句说，如果输入列表看起来像(x:xs)，也就是说，第一个元素x被压缩到剩余的列表xs上，那么它的长度比xs的长度大1。

由于我们根本不使用x，我们也可以用下划线替换它:intListLength (_:xs) = 1 + intListLength xs。

## Combining 多个函数

通过组合许多简单的函数来构建更复杂的函数是很好的Haskell风格。

```hs
-- The number of hailstone steps needed to reach 1 from a starting
-- number.
hailstoneLen :: Integer -> Integer
hailstoneLen n = intListLength (hailstoneSeq n) - 1
```
这对你来说似乎效率很低:它首先生成整个序列，然后找到它的长度，这会浪费很多内存……不是吗?事实上，它没有!由于Haskell的惰性求值，序列的每个元素只在需要时生成，因此序列生成和列表长度计算是交错的。无论序列有多长，整个计算只使用O(1)内存。

## 关于错误消息


